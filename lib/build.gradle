apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def versionStr = '1.0.0'
def libName = 'XmlHelper'
def siteUrl = 'https://github.com/j4velin/XmlHelper'    // Homepage URL of the library
def gitUrl = 'https://github.com/j4velin/XmlHelper.git' // Git repository URL
def group = 'de.j4velin.XmlHelper'                      // Maven Group ID for the artifact

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName versionStr

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    testImplementation 'junit:junit:4.12'
    testImplementation fileTree(dir: "testLibs", include: ["*.jar"])
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact sourcesJar
                artifact javadocJar

                // You can then customize attributes of the publication as shown below.
                groupId = group
                artifactId = 'lib'
                version = versionStr

                pom {
                    name = libName
                    description = 'Android Library for applications to send events to the led_server'
                    url = siteUrl
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'j4velin'
                            name = 'Thomas Hoffmann'
                            email = 'thomas@j4velin.de'
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
} else {
    logger.warn('local.properties file not found')
}

// https://github.com/bintray/gradle-bintray-plugin
version = versionStr
bintray {
    dryRun = true
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['release']
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = libName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            vcsTag = versionStr
            gpg {
                sign = true // Determines whether to GPG sign the files. The default is false
            }
        }
    }
}